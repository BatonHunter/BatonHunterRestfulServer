apply plugin: 'jetty'

sourceCompatibility = 1.8
version = '1.0'

defaultTasks "clean", "fatJar"

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.sparkjava:spark-core:2.2'
    compile 'com.google.code.gson:gson:2.3.1'
    compile 'com.fasterxml.uuid:java-uuid-generator:3.1.3'
    compile 'com.j256.ormlite:ormlite-core:4.48'
    compile 'com.j256.ormlite:ormlite-jdbc:4.48'
    compile 'postgresql:postgresql:9.1-901-1.jdbc4'
    //for testing
    compile 'com.h2database:h2:1.4.187'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

//this task is to compile a jar file which has all the dependencies included
task fatJar(type: Jar) {
    //exclude META-INF to resolve the security exception
    exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA'

    //set main class for the jar file
    manifest {
        attributes 'Implementation-Title': 'Baton Hunter Restful Server',
                'Implementation-Version': version,
                'Main-Class': 'org.batonhunter.server.restful.Main'
    }

    //set jar name so Procfile can pick it up
    baseName = 'BatonHunterRestfulServer-all'

    //including all the dependencies
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }}
    with jar
}

//for Heroku to download and pick up the right gradle wrapper version
task wrapper(type: Wrapper) {
    gradleVersion = '2.0'
}

//for Heroku to run the expected compile tasks
task stage(dependsOn: ['clean', 'fatJar'])